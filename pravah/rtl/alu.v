module alu #(
    parameter OP_WIDTH = 4,
    parameter DATA_WIDTH = 32,
    parameter PHYS_ADDR_W = 6,
    parameter ROB_ADDR_W = 3
) (
    input wire clk_i,   // unused; ALU is combinational
    input wire reset_i, // unused

    // Issue inputs (from RS)
    input wire valid_i,
    input wire [OP_WIDTH-1:0] op_i,
    input wire [DATA_WIDTH-1:0] src1_val_i,
    input wire [DATA_WIDTH-1:0] src2_val_i,
    input wire [DATA_WIDTH-1:0] imm_i,
    input wire use_imm_i,
    input wire [PHYS_ADDR_W-1:0] phys_dest_i,
    input wire [ROB_ADDR_W-1:0] rob_idx_i,

    // Writeback outputs
    output wire result_valid_o,
    output wire [DATA_WIDTH-1:0] result_o,
    output wire [PHYS_ADDR_W-1:0] phys_dest_o,
    output wire [ROB_ADDR_W-1:0] rob_idx_o
);

  wire [DATA_WIDTH-1:0] operand2 = use_imm_i ? imm_i : src2_val_i;
  reg  [DATA_WIDTH-1:0] result_internal;

  always @* begin
    case (op_i)
      4'd0:    result_internal = src1_val_i + operand2;  // ADD/ADDI
      4'd1:    result_internal = src1_val_i - operand2;  // SUB
      4'd2:    result_internal = src1_val_i & operand2;  // AND
      4'd3:    result_internal = src1_val_i | operand2;  // OR
      4'd4:    result_internal = src1_val_i ^ operand2;  // XOR
      4'd5:    result_internal = src1_val_i << operand2[4:0];  // SLL
      4'd6:    result_internal = src1_val_i >> operand2[4:0];  // SRL
      4'd7:    result_internal = ($signed(src1_val_i) < $signed(operand2)) ? 32'd1 : 32'd0;  // SLT
      4'd8:    result_internal = src1_val_i + imm_i;  // ADDI explicitly
      default: result_internal = 32'b0;
    endcase
  end

  // Pass-through outputs
  assign result_valid_o = valid_i;
  assign result_o = result_internal;
  assign phys_dest_o = phys_dest_i;
  assign rob_idx_o = rob_idx_i;

endmodule
